plugins {
    id 'application'

    id 'org.jetbrains.kotlin.jvm' version '1.9.24'
    id 'com.google.protobuf' version '0.8.11'
    id 'org.jmailen.kotlinter' version '3.2.0'
    id 'com.gradleup.shadow' version '8.3.1'
}

group 'no.nav.ereg.sf'
version '0.7'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.ereg.MainKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClassName = 'no.nav.ereg.MainKt'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xms4096m', '-Xmx8192m']
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://packages.confluent.io/maven"
    }

    ["maven-release"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = 'x-access-token'
                password = token
            }
        }
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'com.google.code.gson:gson:2.11.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5"

    implementation "org.http4k:http4k-core:5.14.4.0"
    implementation "org.http4k:http4k-server-netty:5.14.4.0"
    implementation "io.netty:netty-handler:4.1.118.Final" //Transient bump
    implementation "io.netty:netty-common:4.1.118.Final" //Transient bump
    implementation "org.http4k:http4k-client-okhttp:5.14.4.0"

    implementation "org.apache.kafka:kafka-clients:3.9.1"

    implementation "io.grpc:grpc-protobuf:1.69.0"
    implementation "io.grpc:grpc-stub:1.60.0"

    implementation "io.github.microutils:kotlin-logging:1.7.9"
    implementation "ch.qos.logback:logback-classic:1.5.15"
    implementation "net.logstash.logback:logstash-logback-encoder:8.1"

    implementation "io.prometheus:simpleclient_common:0.8.1"
    implementation "io.prometheus:simpleclient_hotspot:0.8.1"

//    implementation "org.apache.commons:commons-compress:1.26.0" //transient http4k-apache
//    implementation "org.apache.httpcomponents:httpclient:4.5.13" //Desired transient httpclient to http4k-apache
//    implementation "org.xerial.snappy:snappy-java:1.1.10.4" //Desired transient snappy to kafka-clients above
//    implementation "org.apache.avro:avro:1.11.4" //Desired transient avro to serializer above
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

project.gradle.startParameter.excludedTaskNames.add('extractIncludeTestProto')

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.0' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.60.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

processResources.dependsOn {
    extractProto
}

processTestResources.dependsOn {
    extractTestProto
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "21"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = "21"
    targetCompatibility = "21"
}