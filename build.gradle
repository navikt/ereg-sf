plugins {
    id 'application'

    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'com.google.protobuf' version '0.8.11'
    id 'org.jmailen.kotlinter' version '5.2.0'
    id 'com.gradleup.shadow' version '8.3.1'
}

group 'no.nav.ereg.sf'
version '0.7'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.ereg.MainKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClassName = 'no.nav.ereg.MainKt'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xms4096m', '-Xmx8192m']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"

    implementation "org.http4k:http4k-core:6.20.1.0"
    implementation "org.http4k:http4k-server-netty:6.20.1.0"
    implementation "org.http4k:http4k-client-okhttp:6.20.1.0"

    implementation "org.apache.kafka:kafka-clients:3.9.1"

    implementation "io.grpc:grpc-protobuf:1.69.0"
    implementation "io.grpc:grpc-stub:1.60.0"

    implementation "io.github.microutils:kotlin-logging:3.0.5"
    implementation "ch.qos.logback:logback-classic:1.5.20"
    implementation "net.logstash.logback:logstash-logback-encoder:8.1"

    implementation "io.prometheus:simpleclient_common:0.8.1"
    implementation "io.prometheus:simpleclient_hotspot:0.8.1"
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

project.gradle.startParameter.excludedTaskNames.add('extractIncludeTestProto')

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.11.0' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.60.0" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

processResources.dependsOn {
    extractProto
}

processTestResources.dependsOn {
    extractTestProto
}

kotlin {
    jvmToolchain(21)
}